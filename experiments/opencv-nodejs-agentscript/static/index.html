<!doctype html>
<html>
<head>
  <title>Shape detection: Node.js, Socket.IO, OpenCV, Canvas</title>
  <link rel="stylesheet" href="//cdn.jsdelivr.net/foundation/4.2.2/css/foundation.min.css">
  <link href='style.css' rel="stylesheet" type="text/css" />
  <meta charset=utf-8>
  <script src="agentscript.min.js"></script>
  <script src="coffee-script.js"></script>
  <script type="text/coffeescript">
    u = ABM.util # ABM.util alias, u.s is also ABM.shape accessor.
    class EnergyModel extends ABM.Model
      setup: ->
        @patches.importColors "images/map-data.png"
        @patches.importDrawing "images/map.png"

        villageImg = document.getElementById('village-sprite')
        windfarmImg = document.getElementById('windfarm-sprite')

        ABM.shapes.add "village", false, (ctx)=>
          ctx.scale(-(1/7), (1/7))
          ctx.rotate Math.PI
          ctx.drawImage(villageImg, -30, -50)
        ABM.shapes.add "windfarm", false, (ctx)=>
          ctx.scale(-(1/7), (1/7))
          ctx.rotate Math.PI
          ctx.drawImage(windfarmImg, -50, -80)

        @agentBreeds "villages windfarms poles"

        @villages.setDefault "shape", "village"
        @villages.setDefault "heading", 0
        @villages.setDefault "size", 1
        @windfarms.setDefault "shape", "windfarm"
        @windfarms.setDefault "heading", 0
        @windfarms.setDefault "size", 1

        @poles.setDefault "shape", "circle"
        @poles.setDefault "color", [0,0,0]
        @poles.setDefault "size", 0.3

        @powerGroups = []

      recompute: false
      power: 0

      setLocations: (breed, locs) ->
        if locs.length then @recompute = true
        for {x, y}, i in locs
          if (i < @[breed].length)
            @[breed][i].moveTo @patches.patchXY(x,y)
          else
            @[breed].create 1, (a)=>
              a.moveTo @patches.patchXY(x,y)

      addPowerLineGroup: (poles) ->
        @powerGroups.push {villages: [], windfarms: []}
        group = @powerGroups.length-1
        for p in poles
          @addPowerLinePole p
          for v in @villages
            if !~@powerGroups[group].villages.indexOf v
              @powerGroups[group].villages.push(v) if v.distance(p) < 6
          for w in @windfarms
            if !~@powerGroups[group].windfarms.indexOf w
              @powerGroups[group].windfarms.push(w) if w.distance(p) < 6

      addPowerLinePole: ({x, y}) ->
        @poles.create 1, (a)=>
          a.moveTo @patches.patchXY(x,y)

      clearPowerlines: ->
        @powerGroups = []
        for p in @poles by -1
          p.die()

      step: ->
        if @recompute
          _power = 0
          for farm in @windfarms
            if farm.p.color[2] > 50 then farm.power = 0             # ocean
            else farm.power = ((255 - farm.p.color[0]) + 45) / 10   # the redder, the less wind
            _power += farm.power
          @power = _power
          showPower @power, "generated"

          for group in @powerGroups
            group.power = 0
            for farm in group.windfarms
              group.power += farm.power
            if group.villages.length > 0
              showPower group.power, "needs"


          @recompute = false

    # div, patchSize, minX, maxX, minY, maxY, isTorus, hasNeighbors
    #   Defaults: 13, -16, 16, -16, 16, false, true
    model = new EnergyModel "layers", 7, 0, 100, 0, 100
    window.model = model
    model.debug() # Debug: Put Model vars in global name space
    model.start() # Run model immediately after startup initialization

    showPower = (power, type) ->
      left = 10 + (power/40 * 180)      # scale power: 0-40MW = 10-190px
      left = Math.min left, 190
      document.querySelectorAll("##{type} .cover")[0].style.left = left+'px'

      left = 1 + (power/40 * 177)
      left = Math.min left, 178
      document.querySelectorAll("##{type} .arrow")[0].style.left = left+'px'

    window.receiveData = (windfarms, villages, powerlines) ->
      scale = 100/240
      for point in windfarms
        point.x = Math.floor point.x * scale
        point.y = 100 - Math.floor point.y * scale
      model.setLocations "windfarms", windfarms

      for point in villages
        point.x = Math.floor point.x * scale
        point.y = 100 - Math.floor point.y * scale
      model.setLocations "villages", villages

      model.clearPowerlines()
      for powerline in powerlines
        if powerline
          for point in powerline
            point.x = Math.floor point.x * scale
            point.y = 100 - Math.floor point.y * scale
          model.addPowerLineGroup powerline
  </script>
</head>
<body>
  <div class="page-wrap">
    <header>
      <div class="row">
        <h1>Learning Everywhere Experiments</h1>
      </div>
    </header>
    <div id="sprites">
      <img id="windfarm-sprite" src="images/windfarm.png" />
      <img id="village-sprite" src="images/village.png" />
    </div>
    <div class="row">
      <div id="layers"></div>
      <canvas id="canvas2" width="320" height="240"></canvas>
      <button id="debugBtn" class="round success">Open Debug</button>
    </div>
  </div>
  <div class="row">
    <div class="large-12 columns">
      <pre id="debug">
      </pre>
    </div>
  </div>
  <div class="footer">
    <div class="row">
      <div class="large-12 columns">
      </div>
    </div>
  </div>

  <div id="results">
    <div>
      City Electrical Needs Met:<br/>
      <div id="needs">
        <div class="scale"><div class="cover"></div><div class="arrow"></div></div>
        <span class="left">0 MW</span><span class="right">40 MW</span>
      </div>
    </div>
    <div>
      Power Generated:<br/>
      <div id="generated">
        <div class="scale"><div class="cover"></div><div class="arrow"></div></div>
        <span class="left">0 MW</span><span class="right">40 MW</span>
      </div>
    </div>
  </div>

  <video id="live" width="320" height="240" style="display: none;" autoplay></video>
  <canvas id="canvas" width="320" height="240" style="display: none;"></canvas>
  <script src="/socket.io/socket.io.js"></script>
  <script src="/shapes-client.js"></script>
</body>
</html>